buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:3.0.1'
        classpath 'io.spring.gradle:propdeps-plugin:0.0.9.RELEASE'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'
    apply plugin: 'org.owasp.dependencycheck'

    dependencyCheck {
        suppressionFile = "${rootProject.projectDir}/suppression.xml"
        outputDirectory = "${rootProject.projectDir}/build/security-report/${project.name}"
        failBuildOnCVSS = 0

        quickQueryTimestamp = false
        analyzers {
            nuspecEnabled = false
            assemblyEnabled = false
            pathToMono = false
        }
    }

    dependencies {
        compile ('javax.validation:validation-api:2.0.0.Final') {
            force = true
        }
        compile ('javax.servlet:javax.servlet-api:4.0.0') {
            force = true
        }
    }

    check.finalizedBy dependencyCheckAnalyze

    configurations {
        all*.exclude group: 'org.hibernate', module: 'hibernate-validator'

        all {
            resolutionStrategy.failOnVersionConflict()
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://jitpack.io"
        }
    }

    idea {
        module {
            downloadSources = true
        }
    }
}
